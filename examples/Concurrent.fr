module examples.Concurrent where

import frege.lib.Random
import Java.Net (URL)

data Thread = mutable native java.lang.Thread where
    native new      :: MutableIO Runnable -> IO Thread
    native start    :: Thread -> IO ()
    native setName  :: Thread -> String -> IO ()
    native getName  :: Thread -> IO String
    native current  java.lang.Thread.currentThread 
                    :: () -> IO Thread
    native sleep java.lang.Thread.sleep
                    :: Long   -> IO () throws InterruptedException

native waitFor   frege.runtime.Runtime.waitFor   :: String -> IO () throws InterruptedException
native notifyOne frege.runtime.Runtime.notifyOne :: String -> IO ()
native notifyAll frege.runtime.Runtime.notifyAll :: String -> IO ()

data IllegalStateException = pure native java.lang.IllegalStateException
derive Exceptional IllegalStateException

data NoSuchElementException = pure native java.util.NoSuchElementException
derive Exceptional NoSuchElementException

data BlockingQueue e = mutable native java.util.concurrent.BlockingQueue where
    --- add element to blocking queue, throw exception if not possible
    native add      :: BlockingQueue e -> e -> IO Bool throws IllegalStateException
    --- add element to blocking queue, return false if not possible
    native offer    :: BlockingQueue e -> e -> IO Bool
    --- add element to blocking queue, block until possible
    native put      :: BlockingQueue e -> e -> IO () throws InterruptedException
    
    --- get and remove element from blocking queue, throw exception if it is empty
    native remove   :: BlockingQueue e -> IO e throws NoSuchElementException
    --- get and remove element from blocking queue, return null if it is empty
    native poll     :: BlockingQueue e -> IO (Maybe e)
    --- get and remove element from blocking queue, block until something is available
    native take     :: BlockingQueue e -> IO e throws InterruptedException

data ArrayBlockingQueue e = mutable native java.util.concurrent.ArrayBlockingQueue where
    native new      :: Int -> IO (ArrayBlockingQueue e)
                     | Int -> Bool ->  IO (ArrayBlockingQueue e)           
    
abstract data MVar a = MV (BlockingQueue a) where
    newEmpty        = ArrayBlockingQueue.new 1 >>= return . MV
    new a           = do m <- newEmpty; m.put a; return m
    put   (MV q) a  = q.put a         
    take  (MV q)    = q.take
    offer (MV q) a  = q.offer a
    poll  (MV q)    = q.poll  
    
-- Haskell compatibility
newEmptyMVar    = MVar.newEmpty
newMVar         = MVar.new 
tryTakeMVar     = MVar.poll
tryPutMVar      = MVar.offer   
                
forkIO :: IO () -> IO Thread
forkIO action = do
    r <- Runnable.new action
    t <- Thread.new r
    t.start
    return t  

main2 args = do
    m <- newEmptyMVar
    forkIO do
        m.put 'x'
        m.put 'y' 
        m.put 'z'
    replicateM_ 3 do
        c <- m.take
        print "got: "
        println c  
        
            
example1 = do
    forkIO (replicateM_ 100000 (putChar 'a'))
    replicateM_ 100000 (putChar 'b')

example2 =  do
    s <- getLine
    case s.long of
        Right n -> forkIO (setReminder n) >> example2
        Left _  -> println ("exiting ...")
    
setReminder :: Long -> IO ()
setReminder n = do
        println ("Ok, I remind you in " ++ show n ++ " seconds")
        Thread.sleep (1000L*n)
        println (show n ++ " seconds is up!")

table = "table"
            
main1 _ = do
    [fork1,fork2,fork3,fork4,fork5] <- mapM MVar.new [1..5]
    forkIO (philosopher "Kant" fork5 fork1)
    forkIO (philosopher "Locke" fork1 fork2)
    forkIO (philosopher "Wittgenstein" fork2 fork3)
    forkIO (philosopher "Nozick" fork3 fork4)
    forkIO (philosopher "Mises" fork4 fork5)
    return ()    

philosopher :: String -> MVar Int -> MVar Int -> IO ()
philosopher me left right = do
    g <- Random.newStdGen
    let phil g  = do
            let (tT,g1) = Random.randomR (60L, 120L) g
                (eT, g2)  = Random.randomR (80L, 160L) g1
                thinkTime = 300L * tT
                eatTime   = 300L * eT
    
            println(me ++ " is going to the dining room and takes his seat.") 
            fl <- left.take            
            println (me ++ " takes up left fork (" ++ show fl ++ ")")
            rFork <- right.poll
            case rFork of
                Just fr -> do 
                    println (me ++ " takes up right fork. (" ++ show fr ++ ")") 
                    println (me ++ " is going to eat for " ++ show eatTime ++ "ms")
                    Thread.sleep eatTime
                    println (me ++ " finished eating.")
                    right.put fr
                    println (me ++ " took down right fork.")
                    left.put fl
                    println (me ++ " took down left fork.")
                    notifyAll table
                    println(me ++ " is going to think for " ++ show thinkTime ++ "ms.")
                    Thread.sleep thinkTime
                    phil g2
                Nothing -> do
                    println (me ++ " finds right fork is already in use.")
                    left.put fl
                    println (me ++ " took down left fork.")
                    notifyAll table
                    println (me ++ " is going to the bar to await notifications from table.")
                    waitFor table
                    println (me ++ " got notice that something changed at the table.")
                    phil g2
            
        inter :: InterruptedException -> IO ()
        inter _ = return ()        
    
    phil g `catch` inter

native openConnection :: URL -> IO URLConnection throws IOException
data URLConnection = mutable native java.net.URLConnection where
    native connect :: URLConnection -> IO () throws IOException
    native getInputStream :: URLConnection -> IO InputStream throws IOException
    
getURL xx = do
    url <- URL.new xx 
    is  <- url.openStream
    ir  <- InputStreamReader.new is "UTF-8"
    br  <- BufferedReader.new ir
    br.getLines
    
main _ =  do
    m1 <- MVar.newEmpty
    m2 <- MVar.newEmpty
    
    forkIO do
        r <- getURL "http://www.wikipedia.org/wiki/Shovel"
        m1.put r
    
    forkIO do
        r <- getURL "http://www.wikipedia.org/wiki/Spade"
        m2.put r
    
    r1 <- m1.take
    r2 <- m2.take
    println (sum (map length r1), sum (map length r2))
    return ()