--- 'Monoid' wrappers for numeric data
module frege.data.wrapper.Num where

import Data.Monoid
import frege.test.QuickCheck

{--
    'Monoid' wrapper for numbers with operation '*' and identity 1
-}
data Product a = Product { unwrap :: a } --- wrap a number
derive Show Product a
derive Eq Product a
derive Ord Product a

--- Haskell compatibility: get the value wrapped by 'Product'
getProduct = Product.unwrap


--- The 'Semigroup' instance for 'Product' uses operation '*'
instance Semigroup Num a => Product a where
    --- > Product 3 <> Product 7 == Product 21@
    Product x `mappend` Product y = Product (x * y)

--- The 'Monoid' instance for 'Product' has identity @1@
instance Monoid Num a => Product a where
    --- > Product 1@
    mempty = Product one

prop_prod_left = 
    forAll arbitrary (\a ->
        mempty <> Product a == Product (1*a))
prop_prod_right =
    forAll arbitrary (\a ->
        Product a <> mempty == Product (a*1))
prop_prod_prod = 
    forAll arbitrary (\(a::Float) ->
        forAll arbitrary (\b ->
            Product a <> Product b == Product (a*b))) 
    
{--
    'Monoid' wrapper for numbers with operation '+' and identity 0
-}   
data Sum a = Sum { unwrap :: a }    --- wrap a number
derive Show Sum a
derive Eq Sum a
derive Ord Sum a

--- Haskell compatibility: get the value wrapped by 'Sum'
getSum = Sum.unwrap


--- The 'Semigroup' instance for 'Sum' uses operation '+'
instance Semigroup Num a => Sum a where
    --- > Sum 19 <> Sum 23 == Sum 42
    Sum x `mappend` Sum y = Sum (x + y)
  
--- The 'Monoid' instance for 'Sum' has identity @0@
instance Monoid Num a => Sum a where
    --- > Sum 0
    mempty = Sum zero

prop_sum_left = 
    forAll arbitrary (\a ->
        mempty <> Sum a == Sum (0+a))
prop_sum_right =
    forAll arbitrary (\a ->
        Sum a <> mempty == Sum (a+0))
prop_sum_sum = 
    forAll arbitrary (\(a::Float) ->
        forAll arbitrary (\b ->
            Sum a <> Sum b == Sum (a+b)))
