{--
    Classes from java.io
-}

protected package frege.java.IO where

import frege.java.Lang public(IOException, OutputStream, PrintStream)
import frege.prelude.PreludeBase
import frege.prelude.PreludeIO
import frege.prelude.PreludeText
import frege.prelude.PreludeList
import frege.prelude.PreludeMonad
import frege.prelude.Arrays

data FileNotFoundException = pure native java.io.FileNotFoundException
derive Exceptional FileNotFoundException

data UnsupportedEncodingException = pure native java.io.UnsupportedEncodingException
derive Exceptional UnsupportedEncodingException

data EOFException = pure native java.io.EOFException where
    pure native new :: String -> EOFException
derive Exceptional EOFException

--- frege equivalent of @java.io.Closeable@    
data Closeable = native java.io.Closeable where    
    native close :: MutIO Closeable -> IO () throws IOException
    -- close :: MutIO Closeable -> IO ()
    -- close x = stderr.println "CLOSABLE.close: Closing something."

data Flushable = native java.io.Flushable where
    native flush :: MutIO Flushable -> IO () throws IOException

--- forward declaration of URI
protected data URI = pure native java.net.URI

data File = native java.io.File where
    native new                :: String -> STMut s File
                              |  Mutable s File -> String -> STMut s File
    pure native toURI         :: File -> URI
    pure native getPathF  getPath    :: File -> String
    
    pure native separator        java.io.File.separator
                              :: String
    pure native pathSeparator    java.io.File.pathSeparator
                              :: String

    native getPath       :: MutIO File -> IO String
    native getName       :: MutIO File -> IO String
    native canRead       :: MutIO File -> IO Bool
    native canWrite      :: MutIO File -> IO Bool
    native isAbsolute    :: MutIO File -> IO Bool
    native isDirectory   :: MutIO File -> IO Bool
    native isFile        :: MutIO File -> IO Bool
    native exists        :: MutIO File -> IO Bool
    native mkdirs        :: MutIO File -> IO Bool
    native delete        :: MutIO File -> IO Bool
    native renameTo      :: MutIO File -> MutIO File -> IO Bool
    native lastModified  :: MutIO File -> IO Long
    native getParentFile :: MutIO File -> IO (Maybe (MutIO File))
    native list          :: MutIO File -> IO (Maybe StringArr)

instance Serializable File

instance Show File where show = File.getPathF


data Writer = native java.io.Writer where
    native write :: MutIO Writer -> Int -> IO () throws IOException
                 |  MutIO Writer -> String -> IO () throws IOException
                 |  MutIO Writer -> String -> Int -> Int -> IO () throws IOException
    putChar :: MutIO Writer -> Char -> IO ()
    putChar w c = write w (ord c)

data OutputStreamWriter = native java.io.OutputStreamWriter where
    native new :: MutIO OutputStream -> String -> IOMut OutputStreamWriter
                                throws UnsupportedEncodingException

data PrintWriter = native java.io.PrintWriter where
    native print    :: MutIO PrintWriter -> String -> IO ()
    native println  :: MutIO PrintWriter -> String -> IO ()
                    |  MutIO PrintWriter -> IO ()
    native new      :: String -> IOMut PrintWriter throws FileNotFoundException
                    |  MutIO File -> IOMut PrintWriter throws FileNotFoundException
                    |  MutIO File -> String -> IOMut PrintWriter 
                                throws FileNotFoundException, UnsupportedEncodingException
                    |  MutIO Writer -> IOMut PrintWriter
                    |  MutIO Writer -> Bool -> IOMut PrintWriter

native stdout frege.runtime.Runtime.stdout :: MutIO PrintWriter
native stderr frege.runtime.Runtime.stderr :: MutIO PrintWriter
native stdin  frege.runtime.Runtime.stdin  :: MutIO BufferedReader
        
{-- 
    Frege type for a @java.io.StringWriter@
    
    Not intended for direct use but rather as something
    a 'PrintWriter' can be made of. (Though, because
    of the 'Appendable' inheritance, one could 
    still 'append' directly.)
    
    To be used like:
    
    > action :: PrintWriter -> IO ()
    > action =  ...
    > actionOnStringWriter :: IO String  
    > actionOnStringWriter = do
    >       sw <- StringWriter.new
    >       pr <- sw.printer
    >       action pr
    >       pr.close
    >       sw.toString  
    -}    
data StringWriter = native java.io.StringWriter where
    --- create a fresh 'StringWriter'
    native new      :: () -> ST s (Mutable s StringWriter)
    --- get the content of a 'StringWriter' as 'String'    
    native toString :: Mutable s StringWriter -> ST s String
    --- make a 'PrintWriter' that prints to this 'StringWriter'
    printer :: MutIO StringWriter -> IOMut PrintWriter
    printer this = PrintWriter.new this -- IOMut PrintWriter

            
-- ----------------------------------------------------------------------------
-- Input Streams & Readers
-- ----------------------------------------------------------------------------

data InputStream = native java.io.InputStream
data FileInputStream = native java.io.FileInputStream where
    native new :: MutIO File -> IOMut FileInputStream 
                    throws FileNotFoundException
                | String  -> IOMut FileInputStream 
                    throws FileNotFoundException

data Reader = native java.io.Reader where
    {--
        Reads a single character and returns it as 'Int' in the range 0 to 65535.
        Returns -1 if the end of the stream has been reached.
        
        Throws 'IOException' if an I/O error occurs.
    -}
    native read :: MutIO Reader -> IO Int throws IOException
    
    {--
        Read the next character from the input stream.
        Throws 'EOFException' if the end of the stream has been reached, or 'IOException' if
        an I/O error occurs.
    -}
    getChar :: MutIO Reader -> IO Char
    getChar rdr = do
        i <- rdr.read
        if i < 0 then throwIO (EOFException.new "getChar") else return (chr i) 


data InputStreamReader = native java.io.InputStreamReader where
    native new :: MutIO InputStream -> String -> IOMut InputStreamReader
                    throws UnsupportedEncodingException
    
    -- native read  :: MutIO InputStream -> IO Int throws IOException
                    
data BufferedReader = native java.io.BufferedReader where
    native new :: MutIO Reader -> IOMut BufferedReader
    {--
        Reads a line of text. A line is considered to be terminated 
        by any one of a line feed ('\n'), a carriage return ('\r'), 
        or a carriage return followed immediately by a linefeed.
        
        [Returns:] 'Just' _string_, where _string_ is the contents of the line, 
        not including any line-termination characters, 
        or 'Nothing' if the end of the stream has been reached.

        [Throws:] IOException - If an I/O error occurs
    -}
    native readLine :: MutIO BufferedReader -> IO (Maybe String)
                    throws IOException
    --- read all lines and return them as list, close reader afterwards
    getLines :: MutIO BufferedReader -> IO [String]
    getLines br = loop [] (repeat br.readLine) where
        loop acc (a:as) = do
            xms <- a
            case xms of
                Just s ->  loop (s:acc) as
                _      ->  br.close >> return (reverse acc)
        loop acc [] = error "cannot happen because (repeat br.readLine) is infinite"
    {-- 
        Reads the next line from a buffered reader using 'readLine', 
        and returns the string or throws 'EOFException' on end of file. 
        -}       
    getLine :: MutIO BufferedReader -> IO String
    getLine br = readLine br >>= maybe (throwIO (EOFException.new "getLine")) return
    

{-- 
    Convenience function to open a file and wrap it with an UTF-8 decoding
    buffered 'Reader'.
    
    May throw 'FileNotFoundException'
    -}

utf8Reader :: String -> IOMut BufferedReader
utf8Reader file = do
    fis <- FileInputStream.new file
    isr <- InputStreamReader.new fis "UTF-8"                    
    BufferedReader.new isr
    