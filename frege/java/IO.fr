{--
    Classes from java.io
-}

protected package frege.java.IO where

import frege.java.Lang public(IOException, PrintStream)
import frege.prelude.PreludeBase
import frege.prelude.PreludeIO
import frege.prelude.PreludeText
import frege.prelude.PreludeList
import frege.prelude.PreludeMonad
import frege.prelude.Arrays

data FileNotFoundException = pure native java.io.FileNotFoundException
derive Exceptional FileNotFoundException

data UnsupportedEncodingException = pure native java.io.UnsupportedEncodingException
derive Exceptional UnsupportedEncodingException

data EOFException = pure native java.io.EOFException where
    pure native new :: String -> EOFException
derive Exceptional EOFException

-- frege equivalent of @java.io.PrintStream@
-- type PrintStream = MutableIO PrintStreamIO

--- frege equivalent of @java.io.OutputStream@
-- type OutputStream = MutableIO OutputStreamIO
data OutputStream = mutable native java.io.OutputStream

--- frege equivalent of @java.io.Closeable@
-- type Closeable = MutableIO CloseableIO    
data Closeable = mutable native java.io.Closeable where    
    native close :: Closeable -> IO () throws IOException

--- frege equivalent of @java.io.Flushable@
-- type Flushable = MutableIO FlushableIO
data Flushable = mutable native java.io.Flushable where
    native flush :: Flushable -> IO () throws IOException

--- forward declaration of URI
protected data URI = pure native java.net.URI

data File = native java.io.File where
    native new                :: String -> STMutable s File
                              |  Mutable s File -> String -> STMutable s File
    pure native toURI         :: File -> URI
    pure native getPathF  getPath    :: File -> String
    
    pure native separator        java.io.File.separator
                              :: String
    pure native pathSeparator    java.io.File.pathSeparator
                              :: String

    native getPath       :: MutableIO File -> IO String
    native getName       :: MutableIO File -> IO String
    native canRead       :: MutableIO File -> IO Bool
    native canWrite      :: MutableIO File -> IO Bool
    native isAbsolute    :: MutableIO File -> IO Bool
    native isDirectory   :: MutableIO File -> IO Bool
    native isFile        :: MutableIO File -> IO Bool
    native exists        :: MutableIO File -> IO Bool
    native mkdirs        :: MutableIO File -> IO Bool
    native delete        :: MutableIO File -> IO Bool
    native renameTo      :: MutableIO File -> MutableIO File -> IO Bool
    native lastModified  :: MutableIO File -> IO Long
    native getParentFile :: MutableIO File -> IO (Maybe (MutableIO File))
    native list          :: MutableIO File -> IO (Maybe (MutableIO StringArr))

instance Serializable File

instance Show File where show = File.getPathF

-- type Writer = MutableIO WriterIO
data Writer = mutable native java.io.Writer where
    native write :: Writer -> Int -> IO () throws IOException
                 |  Writer -> String -> IO () throws IOException
                 |  Writer -> String -> Int -> Int -> IO () throws IOException
    putChar :: Writer -> Char -> IO ()
    putChar w c = write w (ord c)

-- type OutputStreamWriter = MutableIO OutputStreamWriterIO
data OutputStreamWriter = mutable native java.io.OutputStreamWriter where
    native new :: OutputStream -> String -> IO OutputStreamWriter
                                throws UnsupportedEncodingException

-- type PrintWriter = MutableIO PrintWriterIO
data PrintWriter = mutable native java.io.PrintWriter where
    native print    :: PrintWriter -> String -> IO ()
    native println  :: PrintWriter -> String -> IO ()
                    |  PrintWriter -> IO ()
    native new      :: String -> IO PrintWriter throws FileNotFoundException
                    |  MutableIO File -> IO PrintWriter throws FileNotFoundException
                    |  MutableIO File -> String -> IO PrintWriter 
                                throws FileNotFoundException, UnsupportedEncodingException
                    |  Writer -> IO PrintWriter
                    |  Writer -> Bool -> IO PrintWriter

native stdout frege.runtime.Runtime.stdout :: PrintWriter
native stderr frege.runtime.Runtime.stderr :: PrintWriter
native stdin  frege.runtime.Runtime.stdin  :: BufferedReader
        
{-- 
    Frege type for a @java.io.StringWriter@
    
    Not intended for direct use but rather as something
    a 'PrintWriter' can be made of. (Though, because
    of the 'Appendable' inheritance, one could 
    still 'append' directly.)
    
    To be used like:
    
    > action :: PrintWriter -> IO ()
    > action =  ...
    > actionOnStringWriter :: IO String  
    > actionOnStringWriter = do
    >       sw <- StringWriter.new
    >       pr <- sw.printer
    >       action pr
    >       pr.close
    >       sw.toString  
    -}    
data StringWriter = mutable native java.io.StringWriter where
    --- create a fresh 'StringWriter'
    native new      :: () -> IO StringWriter
    --- get the content of a 'StringWriter' as 'String'    
    native toString :: StringWriter -> IO String
    --- make a 'PrintWriter' that prints to this 'StringWriter'
    printer :: StringWriter -> IO PrintWriter
    printer this = PrintWriter.new this -- IOMut PrintWriter

            
-- ----------------------------------------------------------------------------
-- Input Streams & Readers
-- ----------------------------------------------------------------------------

-- type InputStream = MutableIO InputStreamIO
data InputStream = mutable native java.io.InputStream

-- type FileInputStream = MutableIO FileInputStreamIO
data FileInputStream = mutable native java.io.FileInputStream where
    native new :: MutableIO File -> IO FileInputStream 
                    throws FileNotFoundException
                | String  -> IO FileInputStream 
                    throws FileNotFoundException

-- type Reader = MutableIO ReaderIO
data Reader = mutable native java.io.Reader where
    {--
        Reads a single character and returns it as 'Int' in the range 0 to 65535.
        Returns -1 if the end of the stream has been reached.
        
        Throws 'IOException' if an I/O error occurs.
    -}
    native read :: Reader -> IO Int throws IOException
    
    {--
        Read the next character from the input stream.
        Throws 'EOFException' if the end of the stream has been reached, or 'IOException' if
        an I/O error occurs.
    -}
    getChar :: Reader -> IO Char
    getChar rdr = do
        i <- rdr.read
        if i < 0 then throwIO (EOFException.new "getChar") else return (chr i) 

-- type InputStreamReader = MutableIO InputStreamReaderIO
data InputStreamReader = mutable native java.io.InputStreamReader where
    native new :: InputStream -> String -> IO InputStreamReader
                    throws UnsupportedEncodingException
    
-- type BufferedReader = MutableIO BufferedReaderIO                    
data BufferedReader = mutable native java.io.BufferedReader where
    native new :: Reader -> IO BufferedReader
    {--
        Reads a line of text. A line is considered to be terminated 
        by any one of a line feed ('\n'), a carriage return ('\r'), 
        or a carriage return followed immediately by a linefeed.
        
        [Returns:] 'Just' _string_, where _string_ is the contents of the line, 
        not including any line-termination characters, 
        or 'Nothing' if the end of the stream has been reached.

        [Throws:] IOException - If an I/O error occurs
    -}
    native readLine :: BufferedReader -> IO (Maybe String)
                    throws IOException
    --- read all lines and return them as list, close reader afterwards
    getLines :: BufferedReader -> IO [String]
    getLines br = go []  where
        go acc = do
            xms <- br.readLine
            case xms of
                Just s ->  go (s:acc) 
                _      ->  br.close >> return (reverse acc)

    {-- 
        Reads the next line from a buffered reader using 'readLine', 
        and returns the string or throws 'EOFException' on end of file. 
        -}       
    getLine :: BufferedReader -> IO String
    getLine br = readLine br >>= maybe (throwIO (EOFException.new "getLine")) return
    

{-- 
    Convenience function to open a file and wrap it with an UTF-8 decoding
    buffered 'Reader'.
    
    May throw 'FileNotFoundException'
    -}

openReader :: String -> IO BufferedReader
openReader file = do
    fis <- FileInputStream.new file
    isr <- InputStreamReader.new fis "UTF-8"                    
    BufferedReader.new isr

{--
    Convenience function to open a file for writing through an UTF-8 encoding
    'PrintWriter'.
    
    May throw 'FileNotFoundException'
    -}
openWriter :: String -> IO PrintWriter
openWriter name = do
    file <- File.new name
    PrintWriter.new file "UTF-8"
